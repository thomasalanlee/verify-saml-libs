group 'uk.gov.ida'

buildscript {
    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    ext {
        opensaml_version = "3.3.0"
        dropwizard_version = "1.2.0"
        ida_utils_version = '332'
        trust_anchor_version = '1.0-19'
    }
    group = "uk.gov.ida"
    version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }

    configurations {
        slf4j
        guice
        opensaml
        dropwizard
        xml_utils
        test_deps
        security
    }

    dependencies {
        slf4j 'org.slf4j:slf4j-api:1.7.21'

        guice 'com.google.guava:guava:18.0',
                'com.google.inject:guice:4.0'

        dropwizard "io.dropwizard:dropwizard-core:$dropwizard_version",
                "io.dropwizard:dropwizard-client:$dropwizard_version",
                'io.dropwizard:dropwizard-jackson:1.0.5'

        opensaml "org.opensaml:opensaml-core:$opensaml_version",
                "org.opensaml:opensaml-saml-impl:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-api:$opensaml_version",
                "org.opensaml:opensaml-security-api:$opensaml_version",
                "org.opensaml:opensaml-saml-api:$opensaml_version",
                "org.opensaml:opensaml-xmlsec-impl:$opensaml_version",
                'net.shibboleth.utilities:java-support:7.2.0'

        security 'com.nimbusds:nimbus-jose-jwt:5.4',
                "uk.gov.ida.eidas:trust-anchor:$trust_anchor_version",
                "uk.gov.ida:security-utils:2.0.0-$ida_utils_version"

        test_deps 'uk.gov.ida:common-test-utils:2.0.0-38',
                "com.github.tomakehurst:wiremock-standalone:2.5.0",
                "io.dropwizard:dropwizard-testing:$dropwizard_version",
                'org.hamcrest:hamcrest-library:1.3',
                'org.assertj:assertj-joda-time:1.1.0',
                'org.assertj:assertj-core:1.6.0',
                'junit:junit:4.11',
                'uk.gov.ida:ida-dev-pki:1.1.0-21',
                'org.mockito:mockito-core:1.9.5'

        xml_utils "uk.gov.ida:common-utils:2.0.0-$ida_utils_version"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    publishing {
        repositories {
            maven {
                url "/srv/maven" // change to point to your repo, e.g. http://my.org/repo
            }
        }
    }
}
